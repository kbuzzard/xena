/-
DO NOT READ THE FIRST 128 LINES OF THIS FILE IF YOU ARE A BEGINNER.
There is 128 lines of boilerplate. Start at line 128.
-/

import data.mv_polynomial

/-!
# Affine algebraic sets

This file defines affine algebraic subsets of affine n-space and proves basic properties
about them.

## Important definitions

* `affine_algebraic_set k` -- the type of affine algebraic sets over the field `k`.

## Notation

None as yet -- do we need ğ”¸â¿ for affine n-space?

## Implementation notes

Much, but not all, of this file assumes that `k` is algebraically closed.
Remark: analysis/complex/polynomial.lean contains proof that â„‚ is alg closed.

## References

Martin Orr's lecture notes!

## Tags

algebraic geometry, algebraic variety
-/

-- In Lean, the multivariable polynomial ring k[Xâ‚, Xâ‚‚, ..., Xâ‚™] is
-- denoted `mv_polynomial (fin n) k`. We could use better notation.
-- The set kâ¿ is denoted `fin n â†’ k` (which means maps from {0,1,2,...,(n-1)} to k).

-- We now make some definitions which we'll need in the course.

namespace mv_polynomial -- means "multivariable polynomial"

-- let k be a commutative ring
variables {k : Type*} [comm_ring k]

-- and let n be a natural number
variable {n : â„•}

/-- The set of zeros in kâ¿ of a function f âˆˆ k[Xâ‚, Xâ‚‚, ..., Xâ‚™] -/
def zeros (f : mv_polynomial (fin n) k) : set (fin n â†’ k) :=
{x | f.eval x = 0}

/-- x is in the zeros of f iff f(x) = 0 -/
@[simp] lemma mem_zeros (f : mv_polynomial (fin n) k) (x : fin n â†’ k) :
  x âˆˆ f.zeros â†” f.eval x = 0 := iff.rfl

-- note that the next result needs that k is a field. 

/-- The zeros of f * g are the union of the zeros of f and of g -/
lemma zeros_mul {k : Type*} [discrete_field k] (f g : mv_polynomial (fin n) k) :
  zeros (f * g) = zeros f âˆª zeros g :=
begin
  -- two sets are equal if they have the same elements
  ext,
  -- and now it's not hard to prove using `mem_zeros` and other
  -- equalities known to Lean's simplifier.
  simp, -- TODO -- should I give the full proof here?
end

end mv_polynomial

open mv_polynomial

/-- An affine algebraic subset of kâ¿ is the common zeros of a set of polynomials -/
structure affine_algebraic_set (k : Type*) [comm_ring k] (n : â„•) := 
-- a subset of the set of maps {0,1,2,...,n-1} â†’ k (called "carrier")
(carrier : set (fin n â†’ k)) 
-- ...such that there's a set of polynomials such that the carrier is equal to the 
-- intersection of the zeros of the polynomials in the set.
(is_algebraic' : âˆƒ S : set (mv_polynomial (fin n) k), carrier = â‹‚ f âˆˆ S, zeros f) -- ...such that

namespace affine_algebraic_set

-- let k be a commutative ring
variables {k : Type*} [comm_ring k]

-- and let n be a natural number
variable {n : â„•}

-- this is invisible notation so mathematicians don't need to understand the definition
instance : has_coe_to_fun (affine_algebraic_set k n) :=
{ F := Î» _, _,
  coe := carrier
}

-- use `is_algebraic'` not `is_alegbraic` because the notation's right -- no "carrier".
def is_algebraic (V : affine_algebraic_set k n) :
  âˆƒ S : set (mv_polynomial (fin n) k), (V : set _) = â‹‚ f âˆˆ S, zeros f :=
affine_algebraic_set.is_algebraic' V

-- Now some basic facts about affine algebraic subsets. 

/-- Two affine algebraic subsets with the same carrier are equal! -/
lemma ext (V W : affine_algebraic_set k n) : (V : set _) = W â†’ V = W :=
begin
  intro h,
  cases V,
  cases W,
  simpa, -- TODO -- why no debugging output?
end

-- Do I want this instance?
-- /-- We can talk about elements of affine algebraic subsets of kâ¿  -/
-- instance : has_mem (fin n â†’ k) (affine_algebraic_set k n) :=
-- âŸ¨Î» x V, x âˆˆ V.carrierâŸ©

-- Computer scientists insist on using â‰¤ for any order relation such as âŠ† .
-- It is some sort of problem with notation I think. 
instance : has_le (affine_algebraic_set k n) :=
âŸ¨Î» V W, (V : set (fin n â†’ k)) âŠ† WâŸ©
/-- Mathematicians want to talk about affine algebraic subsets of kâ¿
    as being subsets of one another -/
instance : has_subset (affine_algebraic_set k n) := âŸ¨affine_algebraic_set.has_le.leâŸ©

end affine_algebraic_set

-- lecture 1 starts here 

/-
Algebraic geometry lecture 1:

The union of two algebraic sets is an algebraic set.

Kevin Buzzard
-/


/-
# The union of two affine algebraic sets is an affine algebraic set.

Let k be a field and let n be a natural number. We prove the following
theorem in this file:

Theorem. If V and W are two affine algebraic subsets of kâ¿
then their union V âˆª W is also an affine algebraic subset of kâ¿.

Maths proof: if V is cut out by the set S âŠ† k[X_1,X_2,â€¦,X_n]
and W is cut out by T, then we claim V âˆª W is cut out by the set ST := { s*t | s âˆˆ S and t âˆˆ T}.
To prove that the set cut out by ST equals V âˆª W, we prove both inclusions separately.

One inclusion is very easy. If x âˆˆ V âˆª W then either every element of S vanishes at x or every
element of T vanishes on x, and either way every element of ST vanishes at x.

Conversely, if x vanishes at every element of ST, then we want to prove that x âˆˆ V âˆª W. If x is
in V then we're done. If not, then this means that there exists some s âˆˆ S with s(x) â‰  0. 
Then for every t âˆˆ T, we have s(x)t(x)=st(x)=0, and hence t(x)=0, which implies that x is in W.

In Lean the type of `union` is this:

`def union (V W : affine_algebraic_set k n) : affine_algebraic_set k n`

## Implementation notes

I defined an affine algebraic set to be the zeros of an arbitrary
set of functions, as opposed to just a finite set. We will see later
on that these definitions are equivalent.

If V is an affine algebraic set, then V is a pair. The first
element of the pair is a subset V.carrier âŠ† kâ¿, but we will call this set V as well (there is
a coercion from affine algebraic sets to subsets of kâ¿).
The second element of the pair is the proof that there exists a subset S of k[X_1,X_2,...,X_n]
such that V is cut out by S, by which I mean that V is the set of x âˆˆ k^n which vanish
at each element of S.

## References

Martin Orr's lecture notes at https://homepages.warwick.ac.uk/staff/Martin.Orr/2017-8/alg-geom/

## Tags

algebraic geometry, algebraic variety
-/

-- end of docstring; code starts here. 

-- We're proving theorems about affine algebraic sets so the names of the theorems
-- should start with "affine_algebraic_set".
namespace affine_algebraic_set

-- let k now be a field (an integral domain would work just as well)
variables {k : Type*} [discrete_field k]

-- and let n be a natural number
variable {n : â„•}

-- We're working with multivariable polynomials, so let's get access to their notation
open mv_polynomial

-- Now here's a basic fact about affine algebraic sets.

/-- The union of two algebraic subsets of kâ¿ is an algebraic subset-/
def union (V W : affine_algebraic_set k n) : affine_algebraic_set k n :=
{ carrier := V âˆª W, -- the underlying set is the union of the two sets defining V and W
  is_algebraic' :=
  -- We now need to prove that the union of V and W is cut out by some set of polynomials.
  begin
    -- Now here's the bad news. 

    -- Lean notation for kâ¿ is `fin n â†’ k`.
    -- Lean notation for k[Xâ‚, Xâ‚‚, ..., Xâ‚™] is `mv_polynomial (fin n) k`.
    -- Lean notation for the subsets of X is `set X`

    -- Let's state what we're trying to prove, using Lean's notation.
    show 
    âˆƒ (U : set (mv_polynomial (fin n) k)),
      -- such that
      (V : set _) âˆª W = â‹‚ f âˆˆ U, zeros f,
    -- say S is the set that defines V
    cases V.is_algebraic with S hS,
    -- and T is the set that defines W
    -- (slightly fancier way)
    rcases W.is_algebraic with âŸ¨T, hTâŸ©,
    -- Now reduce to an unwieldy precise statement about zeros of polynomials
    rw [hS, hT],
    -- Our goal is now to come up with a set U such that
    -- the zeros of U are exactly the union of the zeros of S and of T.
    -- Here's how to do it.
    use {u | âˆƒ (s âˆˆ S) (t âˆˆ T), u = s * t},
    -- To prove that the affine algebraic set cut out by this collection of polynomials
    -- is precisely the set V âˆª W, we check both inclusions.
    apply set.subset.antisymm,
    { -- Here's the easier of the two inclusions.
      -- say x âˆˆ V âˆª W,
      intros x hx,
      -- it's either in V or W.
      cases hx with hxV hxW,
      { -- Say x âˆˆ V
        -- We know that x vanishes at every element of S.
        rw set.mem_Inter at hxV,
        -- We want to prove x vanishes at every polynomial of the form s * t
        -- with s âˆˆ S and t âˆˆ T.
        rw set.mem_Inter,
        -- so let's take an element u of the form s * t
        rintro u,
        -- Let's now notice that the goal has got completely out of hand, and
        -- simplify it back to âˆ€ s âˆˆ S and âˆ€ t âˆˆ T, (s * t)(x) = 0.
        suffices : âˆ€ s âˆˆ S, âˆ€ t âˆˆ T, u = s * t â†’ u.eval x = 0,
        {rw [set.mem_Inter], rintros âŸ¨s, hsS, t, htT, rflâŸ©, exact this s hsS t htT rfl},
        rintro s hs t ht rfl,
        -- we need to show st(x)=0.
        -- Because x âˆˆ V, we have s(x)=0. 
        have hx := set.mem_Inter.1 (hxV s) hs,
        change s.eval x = 0 at hx,
        -- It suffices to show s(x)*t(x)=0
        rw eval_mul,
        -- but s(x) = 0,
        rw hx,
        -- and now it's obvious
        apply zero_mul,
      },
      { -- This is the case x âˆˆ W and it's essentially completely the same as the x âˆˆ V argument so I won't
        -- comment it. Some sort of argument with the `wlog` tactic might be able to do this.
        rw set.mem_Inter at hxW âŠ¢,
        rintro u,
          suffices : âˆ€ s âˆˆ S, âˆ€ t âˆˆ T, u = s * t â†’ u.eval x = 0,
          {rw [set.mem_Inter], rintros âŸ¨s, hsS, t, htT, rflâŸ©, exact this s hsS t htT rfl},
        rintro s hs t ht rfl,
        have hx := hxW t,
        rw set.mem_Inter at hx,
        replace hx : eval x t = 0 := hx ht,
        rw eval_mul,
        rw hx, simp,
      }
    },
    { -- This is the harder way; we need to check that if x vanishes on every element of S*T, 
      -- then x âˆˆ V or x âˆˆ W.
      intros x hx,
      have hx' : âˆ€ u s : mv_polynomial (fin n) k, s âˆˆ S â†’ âˆ€ t âˆˆ T, u = s * t â†’ u.eval x = 0,
        simpa using hx,
      classical, -- We now proudly assume the law of the excluded middle.
      -- If x âˆˆ V then the result is easy...
      by_cases hx2 : x âˆˆ (V : set _),
        left, rwa â†hS,
      -- ...so we can assume assume x âˆ‰ V,
      -- and hence that there's s âˆˆ S such that s(x) â‰  0
      rw [hS, set.mem_Inter, not_forall] at hx2,
      cases hx2 with s hs,
      have hs2 : s âˆˆ S âˆ§ Â¬eval x s = 0,
        simpa using hs,
      cases hs2 with hsS hns,
      -- we now show x âˆˆ W
      right,
      rw set.mem_Inter,
      -- Say t âˆˆ T
      intro t,
      -- We want to prove that t(x) = 0.
      suffices : t âˆˆ T â†’ eval x t = 0,
        simpa,
      intro ht,
      -- Now by assumption, x vanishes on s * t. 
      replace hx' := hx' (s * t) s hsS t ht rfl,
      -- so s(x) * t(x) = 0
      rw eval_mul at hx',
      -- so either s(x) or t(x) = 0, but we chose s such that s(x) â‰  0.
      cases mul_eq_zero.1 hx' with hxs hxt,
        -- So the case s(x) = 0 is a contradiction
        contradiction,
      -- and t(x) = 0 is what we wanted to prove
      assumption
    }
  end
}
end affine_algebraic_set
